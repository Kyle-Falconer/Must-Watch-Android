buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // Keep this updated to the latest version:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.24.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}



android {
    compileSdkVersion 26
    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    defaultConfig {
        applicationId "com.fullmeadalchemist.mustwatch"

        minSdkVersion 14
        targetSdkVersion 26

        versionCode 2
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries true
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    aaptOptions {
        cruncherEnabled = true
    }
    flavorDimensions "default"
    productFlavors {
        beta {
            dimension "default"
            versionNameSuffix '-beta'
            applicationIdSuffix '.beta'
        }
        prod {
            dimension "default"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            pseudoLocalesEnabled true
        }
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), "proguard-rules.pro"
            signingConfig signingConfigs.release

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }
    }
}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "android.arch.lifecycle:runtime:$arch_version"
    implementation "android.arch.lifecycle:extensions:$arch_version"
    implementation "android.arch.persistence.room:runtime:$arch_version"
    implementation "android.arch.persistence.room:rxjava2:$arch_version"
    implementation 'javax.inject:javax.inject:1'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation 'com.google.code.gson:gson:2.8.1'

    //
    // UNITS OF MEASUREMENT (JSR 363)
    // https://github.com/unitsofmeasurement/unit-api
    // https://github.com/unitsofmeasurement/uom-systems/tree/master/common
    implementation 'javax.measure:unit-api:1.0'
    implementation 'systems.uom:systems-common:0.7.1'
    implementation 'systems.uom:systems-quantity:0.7.1'

    //
    // RxAndroid
    // https://github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'

    //
    // Dagger
    // https://google.github.io/dagger/
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "android.arch.lifecycle:compiler:$arch_version"
    annotationProcessor "android.arch.persistence.room:compiler:$arch_version"

    //
    // DEBUG ONLY
    //
    // http://facebook.github.io/stetho/
    implementation 'com.facebook.stetho:stetho:1.5.0'
    //
    // https://github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.5.1'


    testImplementation "android.arch.persistence.room:testing:$arch_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'

    // Espresso dependencies
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-accessibility:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}

// https://www.thoughtworks.com/insights/blog/signing-open-source-android-apps-without-disclosing-passwords
def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword
