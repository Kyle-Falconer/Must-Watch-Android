buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // Keep this updated to the latest version:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'io.fabric'
//apply plugin: "androidx.navigation.safeargs"

def secretsFile = project.file('secrets.properties')
buildProperties {
    secrets {
        file secretsFile.exists() ? secretsFile : project.file('secrets.properties.example')
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500"
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 27
    signingConfigs {
        release {
            storeFile file(buildProperties.secrets['keyStore'].string)
            storePassword buildProperties.secrets['keyStorePassword'].string
            keyAlias buildProperties.secrets['keyAlias'].string
            keyPassword buildProperties.secrets['keyAliasPassword'].string
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    kapt {
        generateStubs = true
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    defaultConfig {
        applicationId "com.fullmeadalchemist.mustwatch"

        minSdkVersion 14
        targetSdkVersion 27

        versionCode 2
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries true
    }
    aaptOptions {
        cruncherEnabled = true
    }
    flavorDimensions "default"
    productFlavors {
        beta {
            dimension "default"
            versionNameSuffix '-beta'
            applicationIdSuffix '.beta'
        }
        prod {
            dimension "default"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            pseudoLocalesEnabled true
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), "proguard-rules.pro"
            signingConfig signingConfigs.release

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }
    }
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs << "-Xlint:unchecked" << "-Werror"
//}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support:multidex:1.0.3'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:27.1.1"
    implementation "com.android.support:design:27.1.1"
    implementation "com.android.support:appcompat-v7:27.1.1"
    implementation "com.android.support:recyclerview-v7:27.1.1"
    implementation "com.android.support:cardview-v7:27.1.1"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "android.arch.lifecycle:runtime:$arch_lifecycle_version"
    implementation "android.arch.lifecycle:extensions:$arch_lifecycle_version"
    implementation "android.arch.persistence.room:runtime:$arch_persistence_version"
    implementation "android.arch.persistence.room:rxjava2:$arch_persistence_version"
    implementation 'javax.inject:javax.inject:1'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation 'com.google.code.gson:gson:2.8.2'

    //
    // UNITS OF MEASUREMENT (JSR 363)
    // https://github.com/unitsofmeasurement/unit-api
    // https://github.com/unitsofmeasurement/uom-systems/tree/master/common
    implementation 'javax.measure:unit-api:1.0'
    implementation 'systems.uom:systems-common:0.8'
    implementation 'systems.uom:systems-quantity:0.8'

    //
    // RxAndroid
    // https://github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'

    //
    // Dagger
    // https://google.github.io/dagger/
    implementation "com.google.dagger:dagger:2.16"
    kapt "com.google.dagger:dagger-compiler:2.16"

    // Dagger Android
    kapt "com.google.dagger:dagger-android-processor:2.16"
    implementation "com.google.dagger:dagger-android-support:2.16"

    kapt "android.arch.lifecycle:compiler:$arch_lifecycle_version"
    kapt "android.arch.persistence.room:compiler:$arch_persistence_version"

    //
    // DEBUG ONLY
    //
    // http://facebook.github.io/stetho/
    implementation 'com.facebook.stetho:stetho:1.5.0'
    //
    // https://github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.0'


    testImplementation "android.arch.persistence.room:testing:$arch_persistence_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // Espresso dependencies
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-accessibility:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha01"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0-alpha01"

}